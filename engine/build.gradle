apply from: '../config/gradle/common.gradle'
apply plugin: 'java-library'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

import org.reflections.Reflections;
import org.reflections.scanners.SubTypesScanner;
import org.reflections.scanners.TypeAnnotationsScanner;
import org.reflections.util.ConfigurationBuilder;
import org.reflections.util.ClasspathHelper;

// Dependencies needed for what our Gradle scripts themselves use. It cannot be included via an external Gradle file :-(
buildscript {
    repositories {
        // External libs - jcenter is Bintray and is supposed to be a superset of Maven Central, but do both just in case
        jcenter()
        mavenCentral()

        // HACK: Needed for NUI reflections
        mavenLocal()
        google()
        maven { url "http://artifactory.terasology.org/artifactory/virtual-repo-live" }
    }

    dependencies {
        // Needed for caching reflected data during builds
        classpath 'org.reflections:reflections:0.9.10'
        classpath 'dom4j:dom4j:1.6.1'

        // HACK: Needed for NUI reflections
        classpath group: 'org.terasology.nui', name: 'nui', version: '2.0.0-SNAPSHOT'
        classpath group: 'javax.servlet', name: 'javax.servlet-api', version: '3.0.1'
    }
}

dependencies {
    api group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.25'
    api group: 'com.google.code.gson', name: 'gson', version: '2.6.2'

    implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-controllers:$gdxVersion"

    implementation(group: 'org.terasology.gestalt', name: 'gestalt-asset-core', version: '7.0.6-SNAPSHOT')
    implementation(group: 'org.terasology.gestalt', name: 'gestalt-entity-system', version: '7.0.6-SNAPSHOT')
    implementation(group: 'org.terasology.gestalt', name: 'gestalt-module', version: '7.0.6-SNAPSHOT')
    implementation(group: 'org.terasology.gestalt', name: 'gestalt-util', version: '7.0.6-SNAPSHOT')

    implementation group: 'org.terasology.nui', name: 'nui', version: '2.0.0'
    implementation group: 'org.terasology.nui', name: 'nui-libgdx', version: '2.0.0'

    implementation group: 'com.google.protobuf', name: 'protobuf-java', version: '3.4.0'

    implementation "com.github.zafarkhaja:java-semver:0.9.0" // gestalt lost this...
    implementation "com.github.everit-org.json-schema:org.everit.json.schema:1.11.1"
    implementation "com.github.marschall:zipfilesystem-standalone:1.0.1"
    implementation 'dom4j:dom4j:1.6.1'

    //TODO inserted by someone who has no idea how the gradle thing works. Inspect whether necessary (for tests). Copied rom desktop build.gradle. May break things with android (maybe)
    testImplementation "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
    testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    testImplementation "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
    testImplementation "com.badlogicgames.gdx:gdx-controllers-desktop:$gdxVersion"
    testImplementation "com.badlogicgames.gdx:gdx-controllers-platform:$gdxVersion:natives-desktop"

    // Test lib dependencies
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.7.22'
    testImplementation group: 'org.jboss.shrinkwrap', name: 'shrinkwrap-depchain-java7', version: '1.1.3'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.9.0'
}

task cacheReflections {
    description = 'Caches reflection output to make regular startup faster. May go stale and need cleanup at times.'
    inputs.files sourceSets.main.output.classesDirs,
            // getClassesDir from all sourceSets (for any jvm (seems) language)
            configurations."${sourceSets.main.runtimeClasspathConfigurationName}"

    outputs.upToDateWhen {classes.state.upToDate}
    outputs.file("$buildDir/resources/main/reflections.cache")
    dependsOn classes

    doLast {
        // Without the .mkdirs() we might hit a scenario where the classes dir doesn't exist yet
        Reflections reflections = new Reflections(new ConfigurationBuilder()
                .addUrls(inputs.files.collect { it.toURI().toURL() })
                .setScanners(new TypeAnnotationsScanner(), new SubTypesScanner()))
        reflections.save("$buildDir/classes/reflections.cache")
    }
}

jar {
    // This prevents the assets from being scanned as code files
    dependsOn cacheReflections

    archiveName = "sol.jar"

    doFirst {
        copy {
            from 'src/SolAppListener.gwt.xml'
            into 'build/classes/main'
        }
    }
}

eclipse.project {
    name = appName + "-engine"
}

// Extra details provided for unit tests
test {
    // ignoreFailures: Specifies whether the build should break when the verifications performed by this task fail.
    ignoreFailures = true

    // showStandardStreams: makes the standard streams (err and out) visible at console when running tests
    testLogging.showStandardStreams = true
}
